//! Shared material texture sampling and UV transform helpers.

#define_import_path renderide::material::sample

#import renderide::alpha_clip_sample as acs
#import renderide::normal_decode as nd
#import renderide::pbs::normal as pnorm
#import renderide::texture_sampling as ts
#import renderide::uv_utils as uvu

fn sample_uv(raw_uv: vec2<f32>, st: vec4<f32>, polar_power: f32, polar_enabled: bool) -> vec2<f32> {
    let selected_uv = select(raw_uv, uvu::polar_uv(raw_uv, polar_power), polar_enabled);
    return uvu::apply_st(selected_uv, st);
}

fn sample_uv_for_storage(
    raw_uv: vec2<f32>,
    st: vec4<f32>,
    storage_v_inverted: f32,
    polar_power: f32,
    polar_enabled: bool,
) -> vec2<f32> {
    let selected_uv = select(raw_uv, uvu::polar_uv(raw_uv, polar_power), polar_enabled);
    return uvu::apply_st_for_storage(selected_uv, st, storage_v_inverted);
}

fn sample_rgba(
    tex: texture_2d<f32>,
    samp: sampler,
    raw_uv: vec2<f32>,
    st: vec4<f32>,
    lod_bias: f32,
    polar_power: f32,
    polar_enabled: bool,
) -> vec4<f32> {
    return ts::sample_tex_2d(tex, samp, sample_uv(raw_uv, st, polar_power, polar_enabled), lod_bias);
}

fn sample_rgba_lod0(
    tex: texture_2d<f32>,
    samp: sampler,
    raw_uv: vec2<f32>,
    st: vec4<f32>,
    polar_power: f32,
    polar_enabled: bool,
) -> vec4<f32> {
    return acs::texture_rgba_base_mip(tex, samp, sample_uv(raw_uv, st, polar_power, polar_enabled));
}

fn sample_world_normal(
    tex: texture_2d<f32>,
    samp: sampler,
    transformed_uv: vec2<f32>,
    world_normal: vec3<f32>,
    normal_scale: f32,
) -> vec3<f32> {
    let base_normal = normalize(world_normal);
    let tbn = pnorm::orthonormal_tbn(base_normal);
    let tangent_normal = nd::decode_ts_normal_with_placeholder_sample(
        textureSample(tex, samp, transformed_uv),
        normal_scale,
    );
    return normalize(tbn * tangent_normal);
}

fn sample_optional_world_normal(
    tex: texture_2d<f32>,
    samp: sampler,
    transformed_uv: vec2<f32>,
    world_normal: vec3<f32>,
    normal_scale: f32,
    enabled: bool,
) -> vec3<f32> {
    if (!enabled) {
        return normalize(world_normal);
    }
    return sample_world_normal(tex, samp, transformed_uv, world_normal, normal_scale);
}
